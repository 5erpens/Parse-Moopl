/* See Wikipedia page for the quicksort algorithm. */

proc main() {
  arrayof(int) a;
  a = new arrayof(int)[5];
  a[0] = 9;
  a[1] = 1;
  a[2] = 3;
  a[3] = 7;
  a[4] = 5;
  Quicksort qs;
  qs = new object Quicksort();
  qs.sort(a);
  int i;
  i = 0;
  while (i < (a.length)) do {
    output a[i];
    i = i + 1;
  }
}

class Quicksort {
  proc Quicksort() {}

  proc sort(arrayof(int) a) {
    self.quicksort(a, 0, (a.length) - 1);
  }

  proc quicksort(arrayof(int) a, int lo, int hi) {
    if (lo < hi) then {
      int p;
      p = self.partition(a, lo, hi);
      self.quicksort(a, lo, p - 1);
      self.quicksort(a, p + 1, hi);
    } else {}
  }

  fun int partition(arrayof(int) a, int lo, int hi) {
    int pivot;
    pivot = a[hi];
    int i;
    i = lo; // place for swapping
    int j;
    j = lo;
    while (j < hi) do {
      if ((a[j]) < (pivot + 1)) then {
        self.swap(a, i, j);
        i = i + 1;
      } else {}
      j = j + 1;
    }
    self.swap(a, i, hi);
    return i;
  }

  proc swap(arrayof(int) a, int i, int j) {
    int x;
    x = a[i];
    a[i] = a[j];
    a[j] = x;
  }
}
